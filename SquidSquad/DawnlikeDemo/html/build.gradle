buildscript {
	ext.grettyCommit = "749988a"
	repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}
	dependencies {
		// This is a complicated solution to Gradle 7.x being partly incompatible with the latest Gretty 3.x release.
		// Gretty 4.x isn't compatible with Java 8, so it can't be used for many libGDX projects.
		// The Gradle code to depend on a specific Gretty version came from https://github.com/f4lco/gretty-jitpack
		classpath "com.github.gretty-gradle-plugin.gretty:gretty:$grettyCommit"

		configurations.all {
			resolutionStrategy {
				if (org.gradle.util.GradleVersion.current().version.startsWith('7.')) {
					force "org.spockframework:spock-core:2.0-M5-groovy-3.0"
					force "org.gebish:geb-spock:5.0-milestone-1"
				}
			}
		}
		// Instead of the above, we could also use a different, self-made release, but this is probably not optimal.
		//classpath "com.github.tommyettinger.gretty:gretty:3.0.4.5"

	}
}
apply plugin: "gwt"
apply plugin: "war"
apply plugin: "org.gretty"

gwt {
	gwtVersion = "$gwtFrameworkVersion" // Should match the version used for building the GWT backend. See gradle.properties.
	maxHeapSize = '1G' // Default 256m is not enough for the GWT compiler. GWT is HUNGRY.
	minHeapSize = '1G'

	src = files(file('src/main/java')) // Needs to be in front of "modules" below.
	modules 'com.github.tommyettinger.GdxDefinition'
	devModules 'com.github.tommyettinger.GdxDefinitionSuperdev'
	project.webAppDirName = 'webapp'

	compiler.strict = true
	compiler.disableCastChecking = true
	//// The next line can be useful to uncomment if you want output that hasn't been obfuscated.
//	compiler.style = org.wisepersist.gradle.plugins.gwt.Style.DETAILED
}

// This continues the complicated solution to Gretty issues.
configurations.all {
	resolutionStrategy {
		dependencySubstitution.all { dependency ->
			// see https://docs.gradle.org/current/userguide/resolution_rules.html#sub:conditional_dependency_substitution
			if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.group == "org.gretty") {
				dependency.useTarget "com.github.gretty-gradle-plugin.gretty:${dependency.requested.module}:$grettyCommit"
			}
		}
	}
}

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"

//	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
//	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
	implementation 'com.github.tommyettinger:gdx-backend-gwt:1.100.0'
	implementation 'com.github.tommyettinger:gdx-backend-gwt:1.100.0:sources'
//	implementation 'com.github.tommyettinger.gdx-backends:gdx-backend-gwt:3006b7d2d5'
//	implementation 'com.github.tommyettinger.gdx-backends:gdx-backend-gwt:3006b7d2d5:sources'

	implementation "com.github.tommyettinger:jdkgdxds:$jdkgdxdsVersion:sources"
	implementation "com.github.tommyettinger:regexodus:$regexodusVersion:sources"
	implementation "com.github.yellowstonegames.SquidSquad:SquidCore:$squidSquadVersion:sources"
	implementation "com.github.yellowstonegames.SquidSquad:SquidGrid:$squidSquadVersion:sources"
	implementation "com.github.yellowstonegames.SquidSquad:SquidPlace:$squidSquadVersion:sources"
	implementation "com.github.yellowstonegames.SquidSquad:SquidPath:$squidSquadVersion:sources"
	implementation "com.github.yellowstonegames.SquidSquad:SquidText:$squidSquadVersion:sources"
	implementation "com.github.yellowstonegames.SquidSquad:SquidSmooth:$squidSquadVersion:sources"

}

import org.akhikhl.gretty.AppBeforeIntegrationTestTask
import org.wisepersist.gradle.plugins.gwt.GwtSuperDev

gretty.httpPort = 8080
gretty.resourceBase = project.buildDir.path + "/gwt/draftOut"
gretty.contextPath = "/"
gretty.portPropertiesFileName = "TEMP_PORTS.properties"

task startHttpServer (dependsOn: [draftCompileGwt]) {
	doFirst {
		copy {
			from "webapp"
			into gretty.resourceBase
		}
		copy {
			from "war"
			into gretty.resourceBase
		}
	}
}
task beforeRun(type: AppBeforeIntegrationTestTask, dependsOn: startHttpServer) {
    // The next line allows ports to be reused instead of
    // needing a process to be manually terminated.
	file("build/TEMP_PORTS.properties").delete()
	// Somewhat of a hack; uses Gretty's support for wrapping a task in
	// a start and then stop of a Jetty server that serves files while
	// also running the SuperDev code server.
	integrationTestTask 'superDev'
	
	interactive false
}

task superDev(type: GwtSuperDev) {
	doFirst {
		gwt.modules = gwt.devModules
	}
}
var outputPath = "../../../docs/squidsquad/dawnlike/"
task dist(dependsOn: [clean, compileGwt]) {
    doLast {
		file(outputPath).mkdirs()
		copy {
			from("build/gwt/out"){
				exclude '**/*.symbolMap'
			}
			into outputPath
		}
		copy {
			from("webapp") {
				exclude 'index.html'
				exclude '**/refresh.png'
			}
			into outputPath
			}
		copy {
			from("webapp") {
				include 'index.html'
				filter { String line -> line.replaceAll('<a class="superdev" .+', '') }
			}
			into outputPath
			}
		copy {
			from "war"
			into outputPath
		}
	}
}

task addSource {
	doLast {
		sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
	}
}

task distZip(type: Zip, dependsOn: dist){
	//// This uses the output of the dist task, which removes the superdev buttons from index.html .
	from(outputPath)
	archiveBaseName.set("${appName}-dist")
	//// The result will be in html/build/ with a name containing "-dist".
	destinationDir(file("build"))
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)
tasks.checkGwt.dependsOn(addSource)
checkGwt.war = file("war")

sourceCompatibility = 8.0
sourceSets.main.java.srcDirs = [ "src/main/java/" ]

eclipse.project.name = appName + "-html"
